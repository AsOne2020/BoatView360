plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'd452ef7612'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.4.1' apply false
}

preprocess {
    strictExtraMappings = false

    def mc1152_fabric  = createNode('1.15.2-fabric', 1_15_02, '')
	def mc1171_fabric  = createNode('1.17.1-fabric', 1_17_01, '')
    def mc1202_fabric = createNode('1.20.2-fabric', 1_20_02, '')
	def mc1212_fabric = createNode('1.21.2-fabric', 1_21_02, '')

	def mc1152_forge  = createNode('1.15.2-forge', 1_15_02, '')
	def mc1171_forge  = createNode('1.17.1-forge', 1_17_01, '')
    def mc1202_forge = createNode('1.20.2-forge', 1_20_02, '')
	def mc1212_forge = createNode('1.21.2-forge', 1_21_02, '')

	def mc1206_neoforge = createNode('1.20.6-neoforge', 1_20_06, '')
    def mc1212_neoforge = createNode('1.21.2-neoforge', 1_21_02, '')

	mc1152_fabric .link(mc1171_fabric , null)
	mc1171_fabric .link(mc1202_fabric , null)
	mc1202_fabric .link(mc1212_fabric , null)

	mc1152_fabric .link(mc1152_forge , null)
	mc1171_fabric .link(mc1171_forge , null)
	mc1202_fabric .link(mc1202_forge , null)
	mc1212_fabric .link(mc1212_forge , null)

	mc1202_fabric.link(mc1206_neoforge, null)
	mc1212_fabric.link(mc1212_neoforge, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy = DuplicatesStrategy.INCLUDE
			}
		}
	}
}
